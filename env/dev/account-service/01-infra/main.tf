provider "aws" {
  region              = var.aws_region
  allowed_account_ids = [var.aws_accounts.devtest.id]
  default_tags {
    tags = local.tags
  }
}

provider "aws" {
  region              = var.replica_region
  alias               = "replica"
  allowed_account_ids = [var.aws_accounts.devtest.id]
  default_tags {
    tags = local.tags
  }
}

provider "auth0" {
  domain        = var.auth0_domain
  client_id     = var.auth0_client_id
  client_secret = var.auth0_client_secret
}

locals {
  service_name = "accounts"
  tags = {
    Region      = var.aws_region
    Environment = var.environment
    Stage       = var.stage
    Service     = "Account Service"
    Team        = "DevOps"
    CostCenter  = "Development"
  }
  subdomain               = "accounts"
  url                     = "https://${local.subdomain}.${var.stage}.${data.aws_route53_zone.legitscript_zone.name}"
  domain_name             = "${local.service_name}.${var.stage}.${data.aws_route53_zone.legitscript_zone.name}"
  is_private_route53_zone = var.environment != "development"
}

# RDS
module "rds" {
  source = "../../../../modules/applications/account-service/rds"

  providers = {
    aws         = aws
    aws.replica = aws.replica
  }

  name           = local.service_name
  stage          = var.stage
  instance_type  = var.instance_type
  instance_count = var.instance_count
  custom_tags    = var.custom_tags
  engine_version = var.engine_version


  security_groups = [
    var.vpn_security_groups[var.aws_region],
    data.aws_cloudformation_export.account_service_sg.value,
    data.aws_ssm_parameter.circleci_self_hosted_runner_security_group_id.value,
  ]

  # Primary
  vpc_id = data.aws_cloudformation_export.legitscript_vpc_id.value
  private_subnet_ids = [
    data.aws_cloudformation_export.legitscript_service_subnet_1.value,
    data.aws_cloudformation_export.legitscript_service_subnet_2.value,
    data.aws_cloudformation_export.legitscript_service_subnet_3.value,
  ]

  # Replica
  create_cross_region_replica = var.create_cross_region_replica
  replica_vpc_id              = var.create_cross_region_replica ? data.aws_cloudformation_export.replica_legitscript_vpc_id[0].value : null
  replica_private_subnet_ids = var.create_cross_region_replica ? [
    data.aws_cloudformation_export.replica_legitscript_service_subnet_1[0].value,
    data.aws_cloudformation_export.replica_legitscript_service_subnet_2[0].value,
    data.aws_cloudformation_export.replica_legitscript_service_subnet_3[0].value,
  ] : []
  replica_security_groups = var.create_cross_region_replica ? setunion(
    [data.aws_cloudformation_export.replica_account_service_sg[0].value]
  ) : []

  account_service_sql_dump = var.account_service_sql_dump
  skip_final_snapshot      = var.skip_final_snapshot
  deletion_protection      = var.deletion_protection


  db_cluster_parameter_group_family = var.db_cluster_parameter_group_family
  auto_minor_version_upgrade        = var.auto_minor_version_upgrade

  # These variables are used for blue/green deployment
  blue_green                   = var.blue_green
  source_db_cluster_identifier = var.source_db_cluster_identifier

  # We pass this into the module because on personal stages, we restore from a snapshot
  # and the Auth0 API Token that is set in the users table for auth0.noreply@legitscript.com 
  # is not the same as the one that will be generated when this Infra is deployed. 
  # This is a workaround to ensure that the Auth0 User API Token is using the same 
  # one as generated by modules/applications/auth0/account-service-connection
  auth0_api_token = module.account_service_connection.account_service_auth0_api_token
}


# IAM
module "iam" {
  source = "../../../../modules/applications/account-service/iam"

  name      = local.service_name
  stage     = var.stage
  sns_topic = module.sns.user_update_sns_topic_name
}

module "account_service_connection" {
  source = "../../../../modules/applications/auth0/account-service-connection"

  environment         = var.environment
  stage               = var.stage
  vpc_id              = data.aws_cloudformation_export.legitscript_vpc_id.value
  account_service_url = local.url
  auth0_repo_version  = var.auth0_repo_version
  password_policy     = var.auth0_password_policy
}

module "auth0_client" {
  source = "../../../../modules/applications/auth0/client"

  name        = "${var.stage}: Account Service"
  description = "Client to use for local and development environment for the Account Service."

  callbacks           = ["http://localhost:4000/auth/auth0/callback", "${local.url}/auth/auth0/callback"]
  allowed_origins     = ["http://localhost:4000", local.url]
  allowed_logout_urls = ["http://localhost:4000", local.url]
  web_origins         = ["http://localhost:4000", local.url]

  grant_types = [
    "authorization_code",
    "refresh_token",
  ]

  connections = {
    "${var.stage}_account_service" = module.account_service_connection.account_service_connection_id
    legitscript_employee           = data.aws_ssm_parameter.legitscript_employee_connection_id.value
  }
}

module "auth0_management_client" {
  source = "../../../../modules/applications/auth0/client"

  name        = "${var.stage}: Account Service: Management API Access"
  description = "Client to use for local and development environment to grant access to the Auth0 Management APIs."

  audience = "https://${var.auth0_domain}/api/v2/"
  app_type = "non_interactive"
  grant_types = [
    "client_credentials"
  ]

  scopes = [
    "read:users",
    "update:users",
    "read:users_app_metadata",
    "update:users_app_metadata",
    "create:users_app_metadata",
    "read:connections"
  ]
}

# Application Secrets
module "app_secrets" {
  source = "../../../../modules/applications/account-service/secrets"

  # general
  name        = local.service_name
  stage       = var.stage
  environment = var.environment

  # config/application.yml
  enable_asset_pipeline             = var.enable_asset_pipeline
  enable_asset_pipeline_debug       = var.enable_asset_pipeline_debug
  bypass_captcha                    = var.bypass_captcha
  certification_application_url     = var.certification_application_url
  clients_application_url           = var.clients_application_url
  cpv2_application_url              = var.cpv2_application_url
  jwt_expiry                        = var.jwt_expiry
  mailcatcher_host                  = var.mailcatcher_host
  parker_application_url            = var.parker_application_url
  private_application_url           = var.private_application_url
  public_application_url            = var.public_application_url
  risk_application_url              = var.risk_application_url
  scraper_application_url           = var.scraper_application_url
  unassigned_application_url        = var.unassigned_application_url
  test_transactions_application_url = var.test_transactions_application_url
  sns_topic                         = module.sns.user_update_sns_topic_name

  ## auth0
  auth0_application_url   = "https://${data.aws_ssm_parameter.auth0_domain.value}"
  auth0_enabled           = var.auth0_enabled # set in config/application.yml
  auth0_domain            = data.aws_ssm_parameter.auth0_domain.value
  auth0_audience          = data.aws_ssm_parameter.auth0_audience.value
  auth0_redirect_uri      = "${local.url}/auth/auth0/callback"
  auth0_management_domain = var.auth0_domain

  ## secrets
  portal_v2_key_hash             = var.portal_v2_key_hash
  recaptcha_secret_key           = var.recaptcha_secret_key
  recaptcha_site_key             = var.recaptcha_site_key
  secret_token                   = var.secret_token
  auth0_connection               = module.account_service_connection.account_service_connection_name
  auth0_client_id                = module.auth0_client.client_id
  auth0_client_secret            = module.auth0_client.client_secret
  auth0_management_client_id     = module.auth0_management_client.client_id
  auth0_management_client_secret = module.auth0_management_client.client_secret
  iam_access_key_id              = module.iam.access_key_id
  iam_secret_access_key          = module.iam.secret_access_key
  rollbar_access_token           = var.rollbar_access_token

  # config/database.yml
  local_infile          = var.local_infile
  adapter               = var.adapter
  encoding              = var.encoding
  pool                  = var.pool
  reconnect             = var.reconnect
  timeout               = var.timeout
  database_app_password = module.rds.database_app_password
  rds_cluster_endpoint  = module.rds.cluster_endpoint
}


# ACM Cert
module "acm" {
  source       = "../../../../modules/aws/acm"
  domain_name  = local.domain_name
  zone_id      = data.aws_route53_zone.legitscript_zone.zone_id
  private_zone = local.is_private_route53_zone
}

data "aws_route53_zone" "legitscript_zone" {
  name         = var.route53_zone_name
  private_zone = local.is_private_route53_zone
}
